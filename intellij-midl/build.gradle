buildscript {
  ext.kotlin_version = '1.2.31'

  repositories {
    mavenCentral()
    maven { url 'https://jitpack.io'}
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.github.JetBrains:gradle-grammar-kit-plugin:2018.1.1"
  }
}

plugins {
  id 'idea'
  id 'org.jetbrains.intellij' version '0.3.1'
}

group 'net.demurgos'
version '1.0-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

repositories {
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
intellij {
  version '2017.3.5'
}
patchPluginXml {
  changeNotes """
      1.0.0: First release<br>
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}

idea {
  module {
    generatedSourceDirs.add(file("src/gen"))
  }
}

sourceSets {
  main.java.srcDirs = ['src/gen']
}

task generateMidlLexer(type: GenerateLexer) {
  source = "src/main/grammars/MidlLexer.flex"
  targetDir = "src/gen/net/demurgos/midl/lang/lexer"
  targetClass = "MidlLexer"
  purgeOldFiles = true
}

task generateMidlParser(type: GenerateParser) {
  source = "src/main/grammars/MidlParser.bnf"
  targetRoot = "src/gen"
  pathToParser = "/net/demurgos/midl/lang/parser/MidlParser.java"
  pathToPsiRoot = "/net/demurgos/midl/lang/psi"
  purgeOldFiles = true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
  dependsOn(generateMidlLexer, generateMidlParser)
}
